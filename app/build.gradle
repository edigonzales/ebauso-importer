plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.poi.ooxml
    implementation libs.slf4j.api
    implementation libs.logback.classic

    testImplementation libs.junit.jupiter
    testImplementation libs.assertj.core
    testRuntimeOnly libs.junit.platform.launcher
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'ch.so.agi.ebau.importer.App'
}

distributions {
    main {
        distributionBaseName = 'ebauso-importer'
    }
}

tasks.register('fatJar', Jar) {
    archiveFileName = 'ebauso-importer-all.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': application.mainClass.get())
    }
    from(sourceSets.main.output)
    dependsOn(configurations.runtimeClasspath)
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
}
